buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'jp.togashi.android.kkmush'

    compileSdk 33

    defaultConfig {
        applicationId "jp.togashi.android.kkmush"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 7
        versionName '1.3.2'

        buildConfigField 'String', 'GIT_BRANCH', "\"${'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()}\""
        buildConfigField 'String', 'GIT_COMMIT', "\"${'git rev-parse HEAD'.execute([], project.rootDir).text.trim()}\""
        archivesBaseName = "kkmush-${versionName}_${versionCode}"
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    signingConfigs {
        release {}
        if (file('../../signing-release.gradle').exists()) {
            apply from: '../../signing-release.gradle', to: release
        }
    }

    buildTypes {
        debug {}
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation platform('com.google.firebase:firebase-bom:32.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.jakewharton.timber:timber:5.0.1'
}

repositories {
    mavenCentral()
}
